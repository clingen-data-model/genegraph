# Handles SOP7.v2 and SOP8
prefix gci: <http://dataexchange.clinicalgenome.org/gci/>
prefix gcixform: <http://dataexchange.clinicalgenome.org/gcixform/>
prefix sepio: <http://purl.obolibrary.org/obo/SEPIO_>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

construct {
  ?evidenceLine a ?lineType ;
  :sepio/evidence-line-strength-score ?score ;
  :sepio/has-evidence ?evidenceItem , ?functionEvidenceItem ; # evidence about variant
  :sepio/calculated-score ?resultCalculatedScore ;
  :sepio/score-status ?scoreStatus ;
  :dc/date-created ?dateCreated ;
  :dc/description ?evidenceLineDescription .

# Variant evidence item
  ?evidenceItem a ?itemType ;
  :sepio/is-about-allele ?variant ;
  :dc/source ?article ;
  :geno/allele-origin ?alleleOrigin ;
  :sepio/paternity-maternity-confirmed ?paternityMaternityConfirmed ;
  :geno/has-zygosity ?zygosity .
  
# Proband with variant evidence item
  ?individual a :sepio/ProbandWithVariantEvidenceItem ;
  :sepio/is-about-allele ?variant ;
  :sepio/has-sex ?sex ;
  :sepio/age-unit ?ageUnit ;
  :sepio/age-type ?ageType ;
  :sepio/age-value ?ageValue ;
  :sepio/ethnicity ?ethnicity ; 
  :rdfs/label ?individualLabel ;
  :sepio/previous-testing ?prevTesting ;
  :sepio/previous-testing-description ?prevTestingDescription ;
  :sepio/first-testing-method ?firstTestingMethod ;
  :sepio/second-testing-method ?secondTestingMethod ;  
  :sepio/detection-method ?methodsOfDetection ;
  :sepio/has-textual-part ?phenotypeFreeText ;
  :sepio/is-about-condition ?phenotypes ;
  :geno/has-zygosity ?probandZygosity ;
  :sepio/has-variant ?evidenceItem .

  ?family a :pco/Family ;
  :rdfs/label ?familyLabel ;
  :ro/has-member ?individual .

  ?functionEvidenceItem a :sepio/VariantFunctionalImpactEvidenceItem ;
  :dc/description ?functionalDataExplanation ;
  :sepio/functional-data-support ?functionalDataSupport .
}
where {

  ?evidenceLine a gci:variantScore ;
  gci:variantScored ?variant ;
  gci:deNovo ?deNovo ;
  gci:scoreStatus ?scoreStatus ;
  gci:date_created ?date_created ;
  gci:affiliation ?affiliation .
  
  # maschinations due to contradicts score_status do not have gci:caseInfoType
  OPTIONAL {
    ?evidenceLine gci:variantType ?variantType .
    ?variantType gcixform:hasEvidenceLineType ?evidenceLineType ;
    gcixform:hasEvidenceItemType ?evidenceItemType .
  }
  # NonNullVariantEvidenceLine
  BIND(IF(BOUND(?variantType), ?evidenceLineType, sepio:0004097) AS ?lineType) .
  # VariantEvidenceItem
  BIND(IF(BOUND(?variantType), ?evidenceItemType, sepio:0004116) AS ?itemType) .

  OPTIONAL {
    ?evidenceLine gci:calculatedScore ?calculatedScore .
  }

  # This is not actually a proband score, but rather a variant score
  # Should name it as such =tristan
  # BIND (IRI(CONCAT(str(?evidenceLine), "_proband_score_evidence_line")) AS ?probandScoreEvidenceLine) 

  BIND(IF(?deNovo = "Yes", :geno/DeNovoAlleleOrigin , IF(?deNovo = "No", :geno/GermlineAlleleOrigin , :geno/AlleleOrigin ) ) AS ?alleleOrigin) .

  OPTIONAL {
      ?evidenceLine gci:maternityPaternityConfirmed ?paternityMaternityConfirmed .
  }

  OPTIONAL {
    ?evidenceLine gci:functionalDataExplanation ?functionalDataExplanation ;
    BIND(IRI(CONCAT(STR(?evidenceLine), "_function_evidence_item")) AS ?functionEvidenceItem) .
  }

  OPTIONAL {
    ?evidenceLine gci:functionalDataSupport ?functionalDataSupport .
  }

  BIND(IRI(CONCAT(STR(?evidenceLine), "_variant_evidence_item")) AS ?evidenceItem) .

  OPTIONAL {
    ?evidenceLine gci:scoreExplanation ?evidenceLineDescription .
  }
  
  OPTIONAL  {
    ?evidenceLine gci:score ?adjustedScore .
  }
  
  BIND(COALESCE(?adjustedScore, ?calculatedScore) AS ?baseScore) .

  ?individual gci:variantScores ?evidenceLine ;
  gci:label ?individualLabel ;
  gci:proband true .

  OPTIONAL {
    ?individual gci:sex ?sex .
  }

  OPTIONAL {
    ?individual gci:ageType ?ageType .
  }

  OPTIONAL {
    ?individual gci:ageValue ?ageValue .
  }

  OPTIONAL {
    ?individual gci:ageUnit ?ageUnit .
  }

  OPTIONAL {
    ?individual gci:hpoIdInDiagnosis ?phenotypes .
  }

  OPTIONAL {
     ?individual gci:probandIs ?probandZygosity .
  }

  OPTIONAL {
    ?individual gci:ethnicity ?ethnicity .
  }

  ?individual gci:method ?method .

  OPTIONAL {
    ?method gci:genotypingMethods/rdf:rest{0}/rdf:first ?firstTestingMethod .
  }

  OPTIONAL {
    ?method gci:genotypingMethods/rdf:rest{1}/rdf:first ?secondTestingMethod .
  }

  OPTIONAL {
    ?method gci:previousTesting ?prevTesting .
  }

 OPTIONAL {
     ?method gci:specificMutationsGenotypedMethod ?methodsOfDetection .
  }
  
  OPTIONAL {
    ?method gci:previousTestingDescription ?prevTestingDescription .
  }

  OPTIONAL {
     ?individual gci:termsInDiagnosis ?phenotypeFreeText .
  }

  # Below is a score fix based dependent on mode of inheritance, based on the
  # conversation with Gloria Cheung from Slack:
  
  # https://clingen.slack.com/archives/C03TR9RA4DP/p1665181171371129

#   Hi 
# @Tristan Nelson, here is my findings.
# Looks like most of the recessiveZygosity=“Homozygous” are left over data from previous SOPv7.For SOPv8, the recessiveZygosity value for Semidominant curation is evaluated as below in GCI. If probandIs is “Monoallelic heterozygous” or “Hemizygous” then treat as AUTOSOMAL_DOMINANT. And if “Hemizygous” is checked then recessiveZygosity=“Hemizyous”
# So in these cases, recessiveZygosity=“Homozygous” should not be evaluated (is not valid). In SOPv8, if “Hemizygous” is unchecked, recessiveZygosity attribute is removed.
# If probandIs is “Biallelic homozygous” or “Biallelic compound heterozygous” then treat as AUTOSOMAL_RECESSIVE. And recessiveZygosity=“TwoTrans” if “Check here if there are 2 variants AND they are both located in trans with respect to one another” option is checked. And recessiveZygosity=“Homozygous” if “Homozygous” option is checked. So in these cases, recessiveZygosity=“Homozygous” is used to consider how the point is counted. If “Biallelic homozygous” is selected, recessiveZygosity is forced to be “Homozygous”, or if “Biallelic compound heterozygous” is selected and “Homozygous” is checked then in these cases only one variant score can be added and score will be double counted.
# For example, user HOU336_904 and LU_II:7, both have “probandIs”=“Biallelic homozygous” and recessiveZygosity=“Homozygous” and have one variantScore which score is double counted.

  OPTIONAL {
    ?individual gci:recessiveZygosity ?recessiveZygosity .
  }

  OPTIONAL {
    ?individual gci:probandIs ?semidominantZygosity .
  }

  BIND(COALESCE(?semidominantZygosity, ?recessiveZygosity, :geno/Heterozygous ) AS ?zygosity) .
  BIND(IF( ?zygosity = :geno/Homozygous , ?baseScore * 2 , ?baseScore) AS ?score) .
  BIND(IF( ?zygosity = :geno/Homozygous , ?calculatedScore * 2 , ?calculatedScore) AS ?resultCalculatedScore) .

  ?annotation  gci:individuals|gci:individualIncluded ?individual ;
  ^gci:familyIncluded? / ^(gci:families|gci:groups)? / gci:article ?publication .

  OPTIONAL {
    ?family gci:individualIncluded ?individual ;
    gci:label ?familyLabel ;
    a gci:family .
  }

  ?publication gci:pmid ?pmid .
  BIND(IRI(CONCAT(?pmbase, ?pmid)) AS ?article) .

  # TODO remove this before commit 

  # Bind a proband score if MOI is AR. Needed because there is a cap on the proband score
  # in case of autosomal recessive inheritance. 

  # ?gdm a gci:gdm ;
  # gci:modeInheritance ?moistr .

  # BIND(IF(REGEX(?moistr, "0000007"),
  # 	  IRI(CONCAT(STR(?individual), "_proband_evidence_line")),
  # 	  ?undefined)
  #      AS ?proband_evidence_line) .p
  
}
