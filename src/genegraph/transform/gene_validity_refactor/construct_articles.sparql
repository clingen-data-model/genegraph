prefix gci: <http://dataexchange.clinicalgenome.org/gci/>
prefix gcixform: <http://dataexchange.clinicalgenome.org/gcixform/>
prefix ga4gh: <https://terms.ga4gh.org/>
prefix dc: <http://purl.org/dc/terms/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

construct
{
  ?article a dc:BibliographicResource ;
  dc:title ?title ;
  dc:creator ?firstAuthor ;
  dc:date ?year ;
  dc:abstract ?abstract .
}
where
{
  ?gci_article a gci:article ;
  gci:title ?title ;

  gci:authors ?authors ;
  gci:date ?gci_date ;
  gci:pmid ?pmid .

  OPTIONAL {
    ?gci_article gci:abstract ?abstract .
  }
  
  ?authors rdf:first ?firstAuthor .

  BIND(IRI(CONCAT(?pmbase, ?pmid)) AS ?article) .

  BIND(SUBSTR(?gci_date, 1, 4) AS ?year) .
}
