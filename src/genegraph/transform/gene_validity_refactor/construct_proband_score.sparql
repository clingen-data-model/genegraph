prefix gci: <http://gci.clinicalgenome.org/> 
prefix gcixform: <http://dataexchange.clinicalgenome.org/gcixform/>
construct {
  ?evidenceLine a ?evidenceLineType ;
  :sepio/evidence-line-strength-score ?score ;
  :sepio/has-evidence-item ?evidenceItem ;
  :dc/description ?evidenceLineDescription .

  ?evidenceItem a ?evidenceItemType ;
  :sepio/is-about-allele ?allele ;
  :dc/source ?article ;
  :rdfs/label ?individualLabel .

}
where {

  ?evidenceLine a gci:evidenceScore ;
  gci:caseInfoType ?gciCaseType ;
  gci:calculatedScore ?calculatedScore .

  OPTIONAL {
    ?evidenceLine gci:scoreExplanation ?evidenceLineDescription .
  }
  
  ?gciCaseType gcixform:hasEvidenceLineType ?evidenceLineType ;
  gcixform:hasEvidenceItemType ?evidenceItemType .

  OPTIONAL  {
    ?evidenceLine gci:score ?adjustedScore .
  }
  
  BIND(COALESCE(?adjustedScore, ?calculatedScore) AS ?score) .

  ?evidenceItem gci:scores ?evidenceLine ;
  gci:variants ?variant ;
  gci:label ?individualLabel .

  ?annotation  gci:individuals|gci:individualIncluded ?evidenceItem ;
  ^(gci:families|gci:groups)? / gci:article ?publication .
  
  ?publication gci:pmid ?pmid .
  BIND(IRI(CONCAT(?pmbase, ?pmid)) AS ?article) .

  OPTIONAL {
    ?variant gci:carId ?caId .
    BIND(IRI(CONCAT(?arbase, ?caID)) AS ?allele) .
  }

  OPTIONAL {
    ?variant gci:clinvarVariantId ?cvId .
    BIND(IRI(CONCAT(?cvbase, ?cvId)) AS ?allele) .
  }
  
}
