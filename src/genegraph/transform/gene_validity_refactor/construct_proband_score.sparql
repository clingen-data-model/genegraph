# Handles SOP5, SOP6 and SOP7.v1
prefix gci: <http://dataexchange.clinicalgenome.org/gci/>
prefix gcixform: <http://dataexchange.clinicalgenome.org/gcixform/>
construct {
  ?probandScoreEvidenceLine a ?evidenceLineType ;
  :sepio/evidence-line-strength-score ?score ;
  :sepio/has-evidence ?individual ; # evidence about proband
  :sepio/calculatedScore ?calculatedScore ;
  :sepio/score-status ?scoreStatus ;
  :dc/description ?evidenceLineDescription .

# Variant evidence Item
  ?evidenceItem a ?evidenceItemType ;
  :sepio/is-about-allele ?variant ; 
  :dc/source ?article ;
  :geno/allele-origin ?alleleOrigin .

# Proband with variant evidence item
  ?individual a :sepio/ProbandWithVariantEvidenceItem ;
  :sepio/has-sex ?sex ;
  :sepio/age-unit ?ageUnit ;
  :sepio/age-type ?ageType ;
  :sepio/age-value ?ageValue ;
  :sepio/ethnicity ?ethnicity ;
  :rdfs/label ?individualLabel ;
  :sepio/previous-testing ?prevTesting ;
  :sepio/previous-testing-description ?prevTestingDescription ;
  :sepio/testing-methods ?methods ;
  :sepio/has-textual-part ?phenotypeFreeText ;
  :sepio/is-about-condition ?phenotypes ;
  :sepio/has-variant ?evidenceItem .

  ?family a :pco/Family ;
  :rdfs/label ?familyLabel ;
  :ro/has-member ?individual .
}
where {
  # Ensure this pattern does not exist for >= SOPV8 curations
  FILTER NOT EXISTS { ?otherScore a gci:variantScore } .
  
  ?evidenceLine a gci:evidenceScore ;
  gci:caseInfoType ?gciCaseType ;
  gci:scoreStatus ?scoreStatus ;
  gci:date_created ?dateCreated ;
  gci:calculatedScore ?calculatedScore .

  BIND (IRI(CONCAT(str(?evidenceLine), "_proband_score_evidence_line")) AS ?probandScoreEvidenceLine) 

  OPTIONAL {
    ?evidenceLine gci:scoreExplanation ?evidenceLineDescription .
  }
  
  ?gciCaseType gcixform:hasEvidenceLineType ?evidenceLineType ;
  gcixform:hasEvidenceItemType ?evidenceItemType .

  OPTIONAL  {
    ?evidenceLine gci:score ?adjustedScore .
  }
  
  BIND(COALESCE(?adjustedScore, ?calculatedScore) AS ?score) .

  ?individual gci:scores ?evidenceLine ;
  gci:variants ?variant ;
  gci:label ?individualLabel ;

  gci:proband true ;
  gci:sex ?sex ;
  gci:denovo ?deNovo .
  
  BIND(IF(?deNovo = "Yes", :geno/DeNovoAlleleOrigin , IF(?deNovo = "No", :geno/GermlineAlleleOrigin , :geno/AlleleOrigin ) ) AS ?alleleOrigin) .

  OPTIONAL {
     ?individual gci:termsInDiagnosis ?phenotypeFreeText .
  }

  OPTIONAL {
     ?individual gci:ageType ?ageType .
     ?individual gci:ageValue ?ageValue .
     ?individual gci:ageUnit ?ageUnit .  
  }

  OPTIONAL {
     ?individual gci:ethnicity ?ethnicity .
  }

  ?individual gci:method ?method .

  OPTIONAL {
    ?method gci:genotypingMethods ?methods .
  }

  OPTIONAL {
     ?method gci:previousTestingDescription ?prevTestingDescription .
     BIND(IF( bound(?prevTestingDescription) && STRLEN(?prevTestingDescription) > 0, true, false) AS ?prevTesting) .
  }

  OPTIONAL {
     ?individual gci:specificMutationsGenotypedMethod ?methodsOfDetection .
  }

  OPTIONAL {
     ?individual gci:hpoIdInDiagnosis ?phenotypeString .
        BIND(IRI(CONCAT("http://purl.obolibrary.org/obo/HP_", SUBSTR(?phenotypeString, STRLEN(?phenotypeString) - 7 , 7))) AS ?phenotypes) .
  }

  ?annotation  gci:individuals|gci:individualIncluded ?individual ;
  ^gci:familyIncluded? / ^(gci:families|gci:groups)? / gci:article ?publication .

  OPTIONAL {
    ?family gci:individualIncluded ?individual ;
    gci:label ?familyLabel ;
    a gci:family .
  }

  BIND(IRI(CONCAT(STR(?evidenceLine), "_variant_evidence_item")) AS ?evidenceItem) .
  
  ?publication gci:pmid ?pmid .
  BIND(IRI(CONCAT(?pmbase, ?pmid)) AS ?article) .

  # OPTIONAL {
  #   ?variant gci:carId ?caId .
  #   BIND(IRI(CONCAT(?arbase, ?caID)) AS ?allele) .
  # }

  # OPTIONAL {
  #   ?variant gci:clinvarVariantId ?cvId .
  #   BIND(IRI(CONCAT(?cvbase, ?cvId)) AS ?allele) .
  # }
  
}
