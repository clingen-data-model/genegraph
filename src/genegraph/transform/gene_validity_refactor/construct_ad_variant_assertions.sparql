prefix gci: <http://gci.clinicalgenome.org/>

construct
{
  # ?geneticEvidenceCriterionAssessment :sepio/has-evidence-line ?autosomalDominantOtherVariantEvidenceLine ,   ?autosomalDominantNullVariantEvidenceLine ,  ?autosomalDominantDeNovoVariantEvidenceLine .

  ?geneticEvidenceCriterionAssessment :sepio/has-evidence
  ?autosomalDominantOtherVariantCriterionAssessment ,
  ?autosomalDominantNullVariantCriterionAssessment ,
  ?autosomalDominantDeNovoVariantCriterionAssessment .
  
  ?autosomalDominantOtherVariantCriterionAssessment a :sepio/AutosomalDominantOtherVariantCriterionAssessment ;
  :sepio/evidence-line-strength-score ?probandWithOtherVariantTypeWithGeneImpactTotal .

  ?autosomalDominantNullVariantCriterionAssessment a :sepio/AutosomalDominantNullVariantCriterionAssessment ;
  :sepio/evidence-line-strength-score ?probandWithPredictedOrProvenNullVariantTotal .

  ?autosomalDominantDeNovoVariantCriterionAssessment a :sepio/AutosomalDominantDeNovoVariantCriterionAssessment ;
  :sepio/evidence-line-strength-score ?variantIsDeNovoTotal .
}
where
{
  ?classification a gci:provisionalClassification .
  ?classification gci:classificationPoints ?pointsTree .

  BIND (IRI(CONCAT(str(?classification), "_genetic_evidence_criterion_assessment")) AS ?geneticEvidenceCriterionAssessment) .
  
  ?pointsTree gci:autosomalDominantOrXlinkedDisorder ?autosomalDominantOrXlinkedDisorderTree .

  ?autosomalDominantOrXlinkedDisorderTree gci:probandWithOtherVariantTypeWithGeneImpact ?probandWithOtherVariantTypeWithGeneImpactTree .
  ?probandWithOtherVariantTypeWithGeneImpactTree gci:pointsCounted ?probandWithOtherVariantTypeWithGeneImpactTotal .

  ?autosomalDominantOrXlinkedDisorderTree gci:probandWithPredictedOrProvenNullVariant ?probandWithPredictedOrProvenNullVariantTree .
  ?probandWithPredictedOrProvenNullVariantTree gci:pointsCounted ?probandWithPredictedOrProvenNullVariantTotal .

  ?autosomalDominantOrXlinkedDisorderTree gci:variantIsDeNovo ?variantIsDeNovoTree .
  ?variantIsDeNovoTree gci:pointsCounted ?variantIsDeNovoTotal .

  BIND(IRI(CONCAT(str(?classification), "_ad_other_ca")) AS ?autosomalDominantOtherVariantCriterionAssessment) .
  BIND(IRI(CONCAT(str(?classification), "_ad_null_ca")) AS ?autosomalDominantNullVariantCriterionAssessment) .
  BIND(IRI(CONCAT(str(?classification), "_ad_dn_ca")) AS ?autosomalDominantDeNovoVariantCriterionAssessment) .
  
}
