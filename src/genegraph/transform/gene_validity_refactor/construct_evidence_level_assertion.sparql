prefix gci: <http://dataexchange.clinicalgenome.org/gci/>
prefix gcixform: <http://dataexchange.clinicalgenome.org/gcixform/>

construct {
  
  ?classification a :sepio/GeneValidityEvidenceLevelAssertion ;
  :sepio/has-subject ?proposition ;
  :sepio/has-predicate :sepio/HasEvidenceLevel ;
  :sepio/has-object ?evidencelevel ;
  :sepio/evidence-line-strength ?evidencelevel;
  :sepio/qualified-contribution ?contribution ;
  :dc/description ?description ;
  :sepio/has-evidence ?autoClassification ;
  :sepio/is-specified-by ?sopVersion ;
  :sepio/evidence-line-strength-score ?evidencePointsTotal .

  ?legacy_report :bfo/has-part ?classification .

  ?contribution :sepio/has-agent ?affiliation ;
  :bfo/realizes :sepio/ApproverRole ;
  :sepio/activity-date ?date .

  ?autoClassification a :sepio/GeneValidityEvidenceLevelAutoClassification ;
  :sepio/has-subject ?proposition ;
  :sepio/has-predicate :sepio/HasEvidenceLevel ;
  :sepio/has-object ?autoEvidenceLevel ;
  :sepio/evidence-line-strength ?evidencelevel;
  :sepio/evidence-line-strength-score ?evidencePointsTotal .

}
where {
  
  ?proposition a gci:gdm ;
  gci:affiliation ?affiliationID .

  BIND(IRI(CONCAT(?affbase, ?affiliationID)) AS ?affiliation) .
  BIND(IRI(CONCAT(STR(?proposition), "_legacy_report")) AS ?legacy_report) .

  ?classification a gci:provisionalClassification ;
  gci:evidenceSummary ?description_text ;
  gci:autoClassification ?autoEvidenceLevel ;
  gci:alteredClassification ?alteredEvidenceLevel ;

  gci:classificationPoints ?pointsTree .

  OPTIONAL {
    ?classification gci:sopVersion ?declaredSopVersion .
  }

  BIND(COALESCE(?declaredSopVersion, <http://purl.obolibrary.org/obo/SEPIO_0004093>) AS ?sopVersion) .

  OPTIONAL {
    ?classification gci:reasons ?reasonForChangedScore .
  }

  # Trying this out as a way to accomodate changed score
  BIND(IF(bound(?reasonForChangedScore), CONCAT(?description_text, "\n", ?reasonForChangedScore), ?description_text) AS ?description) .
  
  OPTIONAL {
    ?classification gci:classificationDate ?classificationDate .
  }

  OPTIONAL {
    ?classification gci:approvalDate ?approvalDate .
  }

  BIND(COALESCE(?classificationDate, ?approvalDate) AS ?date)

  BIND(IF(?alteredEvidenceLevel = gcixform:NoModification, ?autoEvidenceLevel, ?alteredEvidenceLevel) AS ?evidencelevel) .

  BIND(IRI(CONCAT(STR(?classification), "_contrib")) AS ?contribution ) .

  BIND(IRI(CONCAT(STR(?classification), "_auto_classification")) AS ?autoClassification ) .
  
  ?pointsTree gci:evidencePointsTotal ?evidencePointsTotal .
  
}
